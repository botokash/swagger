#Использовать "../.."

Перем Лог;

Перем ПутьКЛогамПрокси;
Перем ПутьКИсходномуКоду;
Перем КаталогВыгрузкиРезультатов;
Перем ФорматФайлаРезультата;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("src-path", "", "путь к исходному коду конфигурации")
		.ТСтрока();
	Команда.Опция("out", "", "каталог выгрузки результатов")
		.ТСтрока();
	Команда.Опция("format", "", "формат файлов (json или yaml)")
		.ТСтрока();
	Команда.Опция("version", "", "версия OpenAPI")
		.ТСтрока();

КонецПроцедуры

Процедура ПередВыполнениемКоманды(Знач Команда) Экспорт
	
	Лог = ПараметрыПриложения.Лог();

	ПутьКИсходномуКоду = Команда.ЗначениеОпции("src-path");
	КаталогВыгрузкиРезультатов = Команда.ЗначениеОпции("out");
	ФорматФайлаРезультата = Команда.ЗначениеОпции("format");
	ВерсияСпецификации = Команда.ЗначениеОпции("version");

	Лог.Предупреждение("Параметр format не реализован и всегда равен JSON");
	ФорматФайлаРезультата = "json";

	Лог.Предупреждение("Параметр version не реализован и всегда равен 2");
	ВерсияСпецификации = "2";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ПарсерМетаданных = Новый ПарсерМетаданных();

	Если Не ПарсерМетаданных.ПодключитьКонфигурацию(ПутьКИсходномуКоду, "xml") Тогда
		ВызватьИсключение("Не удалось подключить конфигурацию!");
	КонецЕсли;
	
	ПарсерМетаданных.ВыполнитьПарсингКонфигурации();

	Для Каждого HTTPСервис Из ПарсерМетаданных.ВнутренниеОписанияСервисов Цикл
		
		ИмяСервиса = HTTPСервис.Ключ;
		ВнутреннееОписаниеСервиса = HTTPСервис.Значение;

		ГенераторСпецификации = Новый ГенераторСпецификации(ВнутреннееОписаниеСервиса);
		ГенераторСпецификации.ПрочитатьОписаниеСервиса();

		Спецификация = ГенераторСпецификации.ПолучитьСпецификацию();

		КаталогРезультатов = Новый Файл(КаталогВыгрузкиРезультатов);
		АбсолютныйПутьККаталогу = КаталогРезультатов.ПолноеИмя;
		ФС.ОбеспечитьКаталог(АбсолютныйПутьККаталогу);

		ПутьКФайлуРезультата = ОбъединитьПути(АбсолютныйПутьККаталогу, ИмяСервиса + "." + ФорматФайлаРезультата);

		ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлуРезультата, КодировкаТекста.UTF8NoBOM);
		ЗаписьТекста.Записать(Спецификация);
		ЗаписьТекста.Закрыть();

		Лог.Информация("Файл спецификации сгенерирован: " + ПутьКФайлуРезультата);

	КонецЦикла;
		
КонецПроцедуры
